// Library lox:fs

import "lox:assert";
import "lox:collections";

class File {
    construct(path) {
        assert(is(path, "string"), "Parameter to File constructor must be of type string.");

        this._path = _fs_absolute(path, false);
    }

    exists() {
        return _fs_stat_exists(this._path, false);
    }

    write(content, erase) {
        if (this.exists()) {
            return _fs_write_file(this._path, content, erase);
        }

        return false;
    }

    read() {
        if (this.exists()) {
            return _fs_read_file(this._path);   
        }

        return nil;
    }

    rename(name) {
        if (this.exists()) {
            assert(is(name, "string"), "File name must be of type string.");
            if(name = _fs_rename_file(this._path, name)) {
                this._path = name;print name;
                return true;
            }
        }

        return false;
    }

    copy(location) {
        if (this.exists()) {
            assert(is(location, "string"), "File name must be of type string.");
            let to = File(location);
            if (_fs_copy_file(this._path, to.path)) {
                return to;
            }

            return nil;
        }

        return nil;
    }

    create() {
        return _fs_create_file(this._path);
    }

    delete() {
        if (this.exists()) {
            return _fs_delete_file(this._path);
        }

        return false;
    }

    path {
        return this._path;
    }
}

class Directory {
    construct(path) {
        this._files = nil;
        this._path = _fs_absolute(path, true);
    }

    exists() {
        return _fs_stat_exists(this._path, true);
    }

    list() {
        if (this.exists()) {
            if (this._files != nil) {
                return this._files;
            }

            this._files = _fs_list_dir(this._path);
            return this._files;
        }

        return nil;
    }
}

class Stat {
    static exists(path) {
        let isDir = Stat.isDirectory(path);
        path = _fs_absolute(path, isDir);
        return _fs_stat_exists(path, isDir);
    }

    static isFile(path) {
        path = _fs_absolute(path, false);
        return _fs_stat_isfile(path);
    }

    static isDirectory(path) {
        path = _fs_absolute(path, true);
        return _fs_stat_isdir(path);
    }

    static size(path) {
        let isDir = Stat.isDirectory(path);
        path = _fs_absolute(path, isDir);
        if (_fs_stat_exists(path, isDir)) {
            return _fs_stat_size(path); 
        }

        return 0;
    }

}